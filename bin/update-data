#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;
use FindBin '$Bin';
use Log::ger::App;
use Log::ger;

use File::Slurper qw(write_text);
use JSON;
use Mojo::UserAgent;
use Setup::File::TextFragment qw(setup_text_fragment);
use WWW::DistroWatch::ReleaseInfo qw(list_distros get_distro_releases_info);

my $json = JSON->new->pretty(1)->canonical(1);
my $ua = Mojo::UserAgent->new;

my @distros = (
    "debian",
    "ubuntu",
    "mint",
    #"centos",
    "redhat",
    "rocky",
    "alma",
);
for my $distro (@distros) {
    my $time = time();
    my $url = "https://distrowatch.com/table.php?distribution=$distro";
    log_debug("Getting URL '$url' ...");
    my $tx = $ua->get($url);
    unless ($tx->result->is_success) {
        my $err = $tx->error;
        die "Can't get URL '$url': $err->{code} - $err->{message}";
    }
    write_text("$Bin/../raw/${distro}_release/page.html", $tx->res->body);
    write_text("$Bin/../raw/${distro}_release/page.meta", "time: ".scalar(localtime($time))."\nurl: $url\n");
    my $res = get_distro_releases_info(distribution => $distro, file => "$Bin/../raw/${distro}_release/page.html");
    die "Can't get release info for distro '$distro': $res->[0] - $res->[1]" unless $res->[0] == 200;
    my $file = "$Bin/../table/${distro}_release/data.json";
    log_debug("Writing file '$file' ...");
    write_text($file, $json->encode($res->[2]));
}

{
    my $res = list_distros(file => "$Bin/../raw/debian_release/page.html");
    die "Can't get list of distros: $res->[0] - $res->[1]" unless $res->[0] == 200;
    my $file = "$Bin/../table/distro/data.json";
    log_debug("Writing file '$file' ...");
    write_text($file, $json->encode($res->[2]));
}
